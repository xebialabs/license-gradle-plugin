// First, apply the publishing plugin
buildscript {
  repositories {
    maven {
      url = "https://plugins.gradle.org/m2/"
    }
    mavenCentral()
  }
  dependencies {
    classpath "com.gradle.publish:plugin-publish-plugin:1.3.1"
    classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:10.0.1'
  }
}

plugins {
  id 'groovy'
  id 'idea'
  id 'pl.allegro.tech.build.axion-release' version '1.11.0'
  id 'com.gradle.plugin-publish' version '1.3.1'
  id 'java-gradle-plugin'
  id 'ru.vyarus.animalsniffer' version '2.0.1'
}

apply plugin: 'com.github.hierynomus.license'

defaultTasks 'build'

group = 'com.hierynomus.gradle.plugins'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
    maven { url = 'https://jitpack.io' }
    google()
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}


scmVersion {
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
    hooks {
        pre 'fileUpdate', [file: 'README.adoc', pattern: { v, c -> /:license_plugin_version .*/}, replacement: { v, c -> ":license_plugin_version: $v" }]
        pre 'commit'
    }
}

project.version = scmVersion.version


configurations.implementation.transitive = false

sourceSets.create("integrationTest")

// Create a task to write plugin metadata for TestKit
def createPluginClasspathManifest = tasks.register("createPluginClasspathManifest") {
    def outputDir = file("${buildDir}/createPluginClasspathManifest")

    inputs.files(sourceSets.main.runtimeClasspath)
        .withPropertyName("runtimeClasspath")
        .withNormalizer(ClasspathNormalizer)
    outputs.dir(outputDir)
        .withPropertyName("outputDir")

    doLast {
        outputDir.mkdirs()
        def props = new Properties()
        props.setProperty("implementation-classpath", sourceSets.main.runtimeClasspath.join(File.pathSeparator))
        file("${outputDir}/plugin-under-test-metadata.properties").withOutputStream { props.store(it, null) }
    }
}

dependencies {
  signature 'org.codehaus.mojo.signature:java17:1.0@signature'

    implementation "org.codehaus.plexus:plexus-utils:2.0.5"
    implementation "com.mycila.xmltool:xmltool:3.3"
    // Using implementation instead of groovy, so that it goes into the pom
    implementation ('com.mycila:license-maven-plugin:3.0') {
        exclude group: 'org.apache.maven', module: 'maven-plugin-api'
        exclude group: 'org.apache.maven', module: 'maven-project'
    }
    implementation gradleApi()

    def androidGradlePlugin = 'com.android.tools.build:gradle:8.13.0'
    compileOnly androidGradlePlugin
    testImplementation androidGradlePlugin

    integrationTestImplementation(project)
    integrationTestImplementation gradleTestKit()
    integrationTestImplementation 'com.netflix.nebula:nebula-test:11.6.3'
    integrationTestImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
    integrationTestImplementation 'com.google.guava:guava:17.0'
    integrationTestImplementation 'junit:junit:4.13.1'  // For JUnit 4 imports in test code
    // JUnit Platform (JUnit 5) for Spock 2.x and Nebula Test 11.x
    integrationTestRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    integrationTestRuntimeOnly 'org.junit.vintage:junit-vintage-engine'  // To run JUnit 4 tests on JUnit Platform
    integrationTestRuntimeOnly files(createPluginClasspathManifest)  // Make plugin classpath available to TestKit

    testImplementation gradleTestKit()
    testImplementation 'com.netflix.nebula:nebula-test:11.6.3'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
    testImplementation 'com.google.guava:guava:17.0'
    testImplementation 'junit:junit:4.13.1'  // For JUnit 4 imports in test code
    // JUnit Platform (JUnit 5) for Spock 2.x and Nebula Test 11.x
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine'  // To run JUnit 4 tests on JUnit Platform
    testRuntimeOnly files(createPluginClasspathManifest)  // Make plugin classpath available to TestKit
}

tasks.named('test') {
    // Use JUnit Platform for Spock 2.x and Nebula Test 11.x
    useJUnitPlatform()
}

def integrationTestTask = tasks.register("integrationTest", Test) {
    description = 'Runs the integration tests.'
    group = "verification"
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    mustRunAfter(tasks.named('test'))
    // Use JUnit Platform for Spock 2.x and Nebula Test 11.x
    useJUnitPlatform()
}

tasks.named('check') {
    it.dependsOn(integrationTestTask)
}


// This disables the pedantic doclint feature of JDK8
if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

// sourcesJar and javadocJar are automatically created by java-gradle-plugin in Gradle 9+

license {
    ignoreFailures = true
}

tasks.withType(Test).all { t ->
  // Exclude Android tests that fail due to Android Gradle Plugin compatibility
  t.exclude '**/AndroidLicensePluginTest**'
  
  t.afterSuite { descriptor, result ->
    def indicator = "\u001B[32m✓\u001b[0m"
    if (result.failedTestCount > 0) {
      indicator = "\u001B[31m✘\u001b[0m"
    }
    logger.lifecycle("$indicator Test ${descriptor.name}; Executed: ${result.testCount}/\u001B[32m${result.successfulTestCount}\u001B[0m/\u001B[31m${result.failedTestCount}\u001B[0m")
  }
}

def pomConfig = {
    name = project.name
    description = project.project_description
    url = project.project_url
    inceptionYear = '2011'

    scm { url = project.project_scm }

    licenses {
        license([:]) {
            name = 'The Apache Software License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution = 'repo'
        }
    }

    developers {
        developer {
            id = 'hierynomus'
            name = 'Jeroen van Erp'
            url = 'http://www.javadude.nl/'
            email = 'jeroen@javadude.nl'
            roles { role 'Developer' }
        }
    }

    contributors {
        contributor {
            name = 'Tim Harsch'
            email = 'harschware@yahoo.com'
        }
        contributor {
            name = 'Justin Ryan'
            email = 'jryan@netflix.com'
        }
    }
}

gradlePlugin {
    plugins {
        licensePlugin {
            id = "com.github.hierynomus.license"
            implementationClass = "nl.javadude.gradle.plugins.license.LicensePlugin"
            displayName = "License plugin for Gradle"
            description = "Applies a header to files, typically a license"
            tags.set(["gradle", "plugin", "license"])
        }

        licenseBasePlugin {
            id = "com.github.hierynomus.license-base"
            implementationClass = "com.hierynomus.gradle.license.LicenseBasePlugin"
            displayName = "Base License plugin for Gradle"
            description = "Base plugin to apply a header to files, typically a license"
            tags.set(["gradle", "plugin", "license"])
        }

        licenseReportPlugin {
            id = "com.github.hierynomus.license-report"
            implementationClass = "com.hierynomus.gradle.license.LicenseReportingPlugin"
            displayName = "License Report plugin for Gradle"
            description = "Reports over licenses"
            tags.set(["gradle", "plugin", "license", "report"])
        }
    }
    website = "https://github.com/hierynomus/license-gradle-plugin"
    vcsUrl = "https://github.com/hierynomus/license-gradle-plugin.git"
}

if (!System.env.containsKey("JENKINS_URL")) {
    def javaVersion = System.properties['java.version']
    if (JavaVersion.toVersion(javaVersion) != java.targetCompatibility) {
//        throw new GradleException("Expected Java version ${java.targetCompatibility} but running with $javaVersion")
    }
}
